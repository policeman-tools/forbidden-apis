/*
 * (C) Copyright Uwe Schindler (Generics Policeman) and others.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// The following properties need to be defined in gradle.properties next to this file:
//  - forbiddenRootDir
//  - forbiddenClasspath
//  - forbiddenTestClasspath
//  - forbiddenSourceCompatibility

buildscript {
  dependencies {
    classpath fileTree(dir: new File(forbiddenRootDir, 'dist'), include: '*.jar')
  }
}

apply plugin: 'java'
apply plugin: 'de.thetaphi.forbiddenapis'

sourceCompatibility = forbiddenSourceCompatibility

forbiddenApis {
  bundledSignatures = [ 'jdk-unsafe', 'jdk-deprecated', 'jdk-nonportable' ]
  failOnUnsupportedJava = false
}

forbiddenApisMain {
  classesDir = new File(forbiddenRootDir, 'build/main')
  classpath = files(forbiddenClasspath.tokenize(File.pathSeparator))
  bundledSignatures += 'jdk-system-out'
}

forbiddenApisTest {
  classesDir = new File(forbiddenRootDir, 'build/test')
  classpath = files(forbiddenTestClasspath.tokenize(File.pathSeparator))
}
